// MARKDOWN format parser
// Grammar
.md.concat: {$[`v`l~type x;,/o'[x];x]};
.md.indent: {$[1=#x;(1;x);(1+(#[,/x[0]])%2;x[1])]};
// Spacing
.md.sp: <- (\s*)#;
.md.lf: <- ("\r\n" | "\n")#;
// Headings
.md.h1: <- "#"# .md.sp (!.md.lf \.)* .md.lf/{`h1!.md.concat x};
.md.h2: <- "##"# .md.sp (!.md.lf \.)* .md.lf/{`h2!.md.concat x};
.md.h3: <- "###"# .md.sp (!.md.lf \.)* .md.lf/{`h3!.md.concat x};
.md.h4: <- "####"# .md.sp (!.md.lf \.)* .md.lf/{`h4!.md.concat x};
.md.h5: <- "######"# .md.sp (!.md.lf \.)* .md.lf/{`h5!.md.concat x};
.md.h6: <- "#######"# .md.sp (!.md.lf \.)* .md.lf/{`h6!.md.concat x};
.md.hr: <- "---" .md.lf/{`hr!x};
.md.h0: <- .md.h6 | .md.h5 | .md.h4 | .md.h3 | .md.h2 | .md.h1 | .md.hr;
// Terminals
.md.esc1: <- "`"# (!"`"\.)+ "`"# /{`esc1!.md.concat x};
.md.esc2: <- "``"# (!"``"\.)+ "``"#/{`esc2!.md.concat x};
.md.esc3: <- "```"# !.md.lf (!"```"\.)+ "```"#/{`esc3!.md.concat x};
.md.esc4: <- "\\"# \.->esc4;
.md.code: <- "```"# ([a-zA-Z]*)->lang .md.lf ((!(.md.sp* "```")\.)+/{.md.concat x})->body .md.sp* "```"#/{`code!,/x};
.md.esc0: <- .md.code | .md.esc4 | .md.esc3 | .md.esc2 | .md.esc1;
.md.ltxt: <- (.md.esc0 | (!("]" | .md.esc0) \.)+/{.md.concat x})+;
.md.link: <- "["# .md.ltxt "]"# .md.sp "("# ((!")" \.)* /{.md.concat x}) ")"# /{`link!x};
.md.imge: <- "!["# .md.ltxt "]"# .md.sp "("# ((!")" \.)* /{.md.concat x}) ")"# /{`image!x};
.md.bold: <- "**"# (!"**"\.)* "**"#/{`bold!.md.concat x};
.md.curs: <- "_"# (!"_"\.)* "_"#/{`curs!.md.concat x};
// Lists
.md.lsep: <- "  "* (([*\-]) | (\d+ "."))# \s+;
.md.lrow: <- .md.lsep .md.line+/{.md.indent x};
.md.list: <- .md.lrow+/{`list!$[`s`long~type x[0];,x;x]};
// Blocks
.md.brow: <- ">"# ((!.md.lf \.)+/{.md.concat x}) .md.lf;
.md.blck: <- .md.brow+/{`block!x};
// Tables
.md.trow: <- ("|"# ((("\\"# \.) | [^|\r\n])+/{,/x}))+ ("|"#)? .md.lf;
.md.tble: <- (.md.trow/{`thead!x}) .md.trow# (.md.trow/{`trow!x})+ /{`table!x};
.md.term: <- .md.bold | .md.curs | .md.imge | .md.link | .md.esc0;
.md.text: <- (!(.md.term | .md.lf) \.)+/{`text!.md.concat x};
.md.line: <- !(.md.lsep | .md.brow) (.md.term | .md.text)+ .md.lf;
// Paragraph
.md.head: <- .md.h0 | .md.tble | .md.list | .md.blck;
.md.para: <- .md.line+ /{`para!x};
.md.pars: <- ((.md.lf* .md.head) | (.md.lf+ .md.para))* .md.lf*/{$[`v`l~type x;x;,x]};
// -- End grammar
