// MARKDOWN format parser
// Grammar
.md.concat: {$[`v`l~type x;,/o'[x];x]};
.md.indent: {$[1=#x;(1;x);(1+(#[,/x[0]])%2;x[1])]};
// Spacing
.md.sp: <- (\s*)#;
.md.lf: <- ("\r\n" | "\n")#;
// Headings
.md.h1: <- "#"# .md.sp (!.md.lf \.)* .md.lf/{`h1!.md.concat x};
.md.h2: <- "##"# .md.sp (!.md.lf \.)* .md.lf/{`h2!.md.concat x};
.md.h3: <- "###"# .md.sp (!.md.lf \.)* .md.lf/{`h3!.md.concat x};
.md.h4: <- "####"# .md.sp (!.md.lf \.)* .md.lf/{`h4!.md.concat x};
.md.h5: <- "######"# .md.sp (!.md.lf \.)* .md.lf/{`h5!.md.concat x};
.md.h6: <- "#######"# .md.sp (!.md.lf \.)* .md.lf/{`h6!.md.concat x};
.md.hr: <- "---" .md.lf/{`hr!x};
.md.h0: <- .md.h6 | .md.h5 | .md.h4 | .md.h3 | .md.h2 | .md.h1 | .md.hr;
// Terminals
.md.esc1: <- "`"# (!"`"\.)+ "`"# /{`esc1!.md.concat x};
.md.esc2: <- "``"# (!"``"\.)+ "``"#/{`esc2!.md.concat x};
.md.esc3: <- "```"# (!"```"\.)+ "```"#/{`esc3!.md.concat x};
.md.esc4: <- "\\"# \.->esc4;
.md.code: <- "```"# ([a-zA-Z]+)->lang .md.lf .md.sp* ((!"```"\.)*/{.md.concat x})->body "```"#/{`code!,/x};
.md.esc0: <- .md.code | .md.esc4 | .md.esc3 | .md.esc2 | .md.esc1;
.md.ltxt: <- (.md.esc0 | (!("]" | .md.esc0) \.)+/{.md.concat x})+;
.md.link: <- "["# .md.ltxt "]"# .md.sp "("# ((!")" \.)* /{.md.concat x}) ")"# /{`link!x};
.md.bold: <- "**"# (!"**"\.)* "**"#/{`bold!.md.concat x};
// Lists
.md.lsep: <- "  "* (([*\-]) | (\d+ "."))# \s+;
.md.lrow: <- .md.lsep .md.line+/{.md.indent x};
.md.list: <- .md.lrow+/{`list!$[`s`long~type x[0];,x;x]};
// Tables
.md.trow: <- ("|"# ((("\\"# \.) | [^|\r\n])+/{,/x}))+ ("|"#)? .md.lf;
.md.tble: <- (.md.trow/{`thead!x}) .md.trow# (.md.trow/{`trow!x})+ /{`table!x};
.md.term: <- .md.bold | .md.link | .md.esc0;
.md.text: <- (!(.md.term | .md.lf) \.)+/{`text!.md.concat x};
.md.line: <- !.md.lsep (.md.term | .md.text)+ .md.lf;
// Paragraph
.md.head: <- .md.h0 | .md.tble | .md.list;
.md.para: <- .md.line+ /{`block!x};
.md.pars: <- ((.md.lf* .md.head) | (.md.lf+ .md.para))* .md.lf*/{$[`v`l~type x;x;,x]};
// -- End grammar

// HTML conversions

// Create nested struct
.md.ht.list: {[rows]
    .ind:: 0;
    val: ,/
    {[row]
        (ind;val): row;
        if [ind=.ind] {
            format["<li>%</li>";,/.md.ht.build[,val]]
        } elif [ind>.ind] {
            ul: ,/{"<ul>"}'!ind - .ind;
            .ind:: ind;
            format["%<li>%</li>";ul;,/.md.ht.build[,val]]
        } else {
            ul: ,/{"</ul>"}'!.ind - ind;
            .ind:: ind;
            format["%<li>%</li>";ul;,/.md.ht.build[,val]]
        }
    }'rows;
    $[0<.ind;format["%%";val;,/{"</ul>"}'!.ind];val]
};

.md.ht.pop: {[item]
    $[`v`l~type item;*item;item]
};

// Build/Load
.md.ht.build: {[content]
    ,/
    {[record]
        if [`s`char~type record] {
            record
        } elif [`v`char~type record] {
            record
        } elif [`v`l~type record] {
            ,/o'record
        } else {
            (tag;text): (!record;. record);
            match [*tag] {
                `h1    -> format["<h1>%</h1>";.md.ht.pop text];
                `h2    -> format["<h2>%</h2>";.md.ht.pop text];
                `h3    -> format["<h3>%</h3>";.md.ht.pop text];
                `h4    -> format["<h4>%</h4>";.md.ht.pop text];
                `h5    -> format["<h5>%</h5>";.md.ht.pop text];
                `h6    -> format["<h6>%</h6>";.md.ht.pop text];
                `hr    -> "<hr></hr>";
                `esc1  -> format["<pre class=\"code-inline\"><code>%</code></pre>";.md.ht.pop text];
                `esc2  -> format["<pre class=\"code-inline\"><code>%</code></pre>";.md.ht.pop text];
                `esc3  -> format["<pre class=\"code-inline\"><code>%</code></pre>";.md.ht.pop text];
                `esc4  -> format["%";.md.ht.pop text];
                `code  -> format["<pre class=\"code\"><code>%</code></pre>";(.md.ht.pop text)`body];
                `link  -> format["<a href=\"%\">%</a>";ssr[(.md.ht.pop text)1;"md";"html"];o[(.md.ht.pop text)0]];
                `bold  -> format["<strong>%</strong>";.md.ht.pop text];
                `list  -> .md.ht.list[.md.ht.pop text];
                `text  -> format["%";.md.ht.pop text];
                `block -> format["<div><p>%</p></div>";$[1<#t:.md.ht.pop text;,/o't;o[t]]];
                `table -> format["<table>%</table>";,/o'[.md.ht.pop text]];
                `thead -> format["<tr>%</tr>";,/format["<th>%</th>";]'[.md.ht.pop text]];
                `trow  -> format["<tr>%</tr>";,/format["<td>%</td>";]'[.md.ht.pop text]];
                _      -> format["<unknown>%</unknown>";.md.ht.pop text];
            }
        }
    }'content
};